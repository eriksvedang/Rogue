
var margin = 5
var w = 20
var h = 10

var px = Int(w / 2)
var py = Int(h / 2)
var levelRoughness = 5

array level

ClearText()
DisplayGraphics()

#Print(" R O G U E L I K E ")

GenerateLevel()
Draw()

loop
  Move()
end

Print("GAME OVER")

void Move()
  bool didMove = false
  if IsKeyPressed("left") and CanWalk(-1, 0)
    px -= 1
    didMove = true
  else if IsKeyPressed("right") and CanWalk(1, 0)
    px += 1
    didMove = true
  else if IsKeyPressed("up") and CanWalk(0, -1)
    py -= 1
    didMove = true
  else if IsKeyPressed("down") and CanWalk(0, 1)
    py += 1
    didMove = true
  end

  if didMove
    Text(px + margin, py + margin, "+")
    DisplayGraphics()
    Draw()
    CheckForEdge()
  end
end

void Draw()
  Color(1,1,1)
  loop y from 0 to h - 1
    loop x from 0 to w - 1
      var row = level[y]
      var tile = row[x]
      if tile == "."
        Color(0,1,0.2)
      else if tile == "X"
        Color(1,1,0)
      else if tile == "#"
        Color(0.3,0.3,0.3)
      end
      Text(x + margin, y + margin, row[x])
    end
  end
  
  Color(1,1,1)
  Text(px + margin, py + margin, "@")

  DisplayGraphics()
end

bool CanWalk(number dx, number dy)
  number x = px + dx
  number y = py + dy
  
  if x < 0
    return true
  else if x > w - 1
    return true
  else if y < 0
    return true
  else if y > h - 1
    return true
  end

  var row = level[y]
  return row[x] != "#"
end

void CheckForEdge()
  bool teleported = false

  if px < 0
    px = w - 1
    teleported = true
  else if px > w - 1
    px = 0
    teleported = true
  end

  if py < 0
    py = h - 1
    teleported = true
  else if py > h - 1
    py = 0
    teleported = true
  end

  if teleported
    ClearText()
    GenerateLevel()
    Draw()
  end
end

void GenerateLevel()
  loop y from 0 to h - 1
    level[y] = []
    loop x from 0 to w - 1
      SetLevel(x, y, RandomTerrain(levelRoughness))
    end
  end

  loop from 1 to 4
    PlaceEnemy()
  end
end

void SetLevel(number x, number y, string s)
  var row = level[y]
  row[x] = s
end

string RandomTerrain(number roughness)
   var r = Int(Random() * roughness)
   if r == 0
     return "."
   else if r < 4
     return " "
   else
     return "#"
   end
end

void PlaceEnemy()
  var x = Int(Random() * w)
  var y = Int(Random() * h)
  SetLevel(x, y, "X")
end



