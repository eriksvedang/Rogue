
var margin = 3
var w = 20
var h = 10

var px = Int(w / 2)
var py = Int(h / 2)
var hp = 10

var levelRoughness = 5

array level

ClearText()
DisplayGraphics()

#Print(" R O G U E L I K E ")

GenerateLevel()
Draw()

loop
  Move()
end

Print("GAME OVER")

void Move()
  number newX = px
  number newY = py

  bool didMove = false
  if IsKeyPressed("left") and CanWalk(-1, 0)
    newX = px - 1
    didMove = true
  else if IsKeyPressed("right") and CanWalk(1, 0)
    newX = px + 1
    didMove = true
  else if IsKeyPressed("up") and CanWalk(0, -1)
    newY = py - 1
    didMove = true
  else if IsKeyPressed("down") and CanWalk(0, 1)
    newY = py + 1
    didMove = true
  end

  if didMove
    Text(newX + margin, newY + margin, "+")
    DisplayGraphics() # make the sword appear immediately
    bool lost = MaybeFight(newX, newY)
    if lost
      # do nothing
    else
      px = newX
      py = newY
    end
    AI()
    Draw()
    CheckForEdge()
  end
end

void AI()
  
end

# Returns true if you lost the battle
bool MaybeFight(number x, number y)
  if x < 0
    return false
  else if x > w - 1
    return false
  else if y < 0
    return false
  else if y > h - 1
    return false
  end

  var row = level[y]
  var tile = row[x]
  if tile == "X"
    return Fight(x, y, 1)
  end
  return false
end

bool Fight(number x, number y, number enemyLevel)  
  loop a from 1 to 10
    Color(a / 10.0,1 - a / 10.0,0)
    Text(1, 0, "Attack!")
    DisplayGraphics()
    Sleep(0.1)
  end

  if Random() > 0.3
    SetLevel(x, y, " ")
    Color(0,1,0)
    Text(1, 1,"Enemy defeated!")
    DisplayGraphics()
    Sleep(1)
    return false
  else
    Color(0,0,1)
    Text(1, 1, "You missed")
    DisplayGraphics()
    Sleep(1)
    return true
  end
end

void Draw()
  Text(0,0, "                    ")
  Text(0,1, "                    ")
  Text(0,2, "                    ")

  Color(1,1,1)
  loop y from 0 to h - 1
    loop x from 0 to w - 1
      var row = level[y]
      var tile = row[x]
      if tile == "."
        Color(0,1,0.2)
      else if tile == "X"
        Color(1,1,0)
      else if tile == "#"
        Color(0.3,0.3,0.3)
      end
      Text(x + margin, y + margin, row[x])
    end
  end
  
  Color(1,1,1)
  Text(px + margin, py + margin, "@")

  Color(1,1,1)
  Text(margin, margin + h + 2, "HP: " + hp)

  DisplayGraphics()
end

bool CanWalk(number dx, number dy)
  number x = px + dx
  number y = py + dy
  
  if x < 0
    return true
  else if x > w - 1
    return true
  else if y < 0
    return true
  else if y > h - 1
    return true
  end

  var row = level[y]
  var tile = row[x]

  if tile == "#"
    return false
  end

  return true
end

void CheckForEdge()
  bool teleported = false

  if px < 0
    px = w - 1
    teleported = true
  else if px > w - 1
    px = 0
    teleported = true
  end

  if py < 0
    py = h - 1
    teleported = true
  else if py > h - 1
    py = 0
    teleported = true
  end

  if teleported
    ClearText()
    GenerateLevel()
    Draw()
  end
end

void GenerateLevel()
  loop y from 0 to h - 1
    level[y] = []
    loop x from 0 to w - 1
      SetLevel(x, y, RandomTerrain(levelRoughness))
    end
  end

  loop from 1 to 4
    PlaceEnemy()
  end
end

void SetLevel(number x, number y, string s)
  var row = level[y]
  row[x] = s
end

string RandomTerrain(number roughness)
   var r = Int(Random() * roughness)
   if r == 0
     return "."
   else if r < 4
     return " "
   else
     return "#"
   end
end

void PlaceEnemy()
  var x = Int(Random() * w)
  var y = Int(Random() * h)
  SetLevel(x, y, "X")
end



