
var margin = 3
var w = 20
var h = 10

var px = Int(w / 2)
var py = Int(h / 2)
var hp = 10

array enemies = []
var levelRoughness = 5

array level

ClearText()
DisplayGraphics()

#Print(" R O G U E L I K E ")

GenerateLevel()
Draw()

loop
  Move()
end

Print("GAME OVER")

void Move()
  Color(0,1,1)
  Text(px + margin, py + margin, "@")
  DisplayGraphics()

  number newX = px
  number newY = py

  bool didMove = false
  if IsKeyPressed("left") and CanWalk(px - 1, py, true)
    newX = px - 1
    didMove = true
  else if IsKeyPressed("right") and CanWalk(px + 1, py, true)
    newX = px + 1
    didMove = true
  else if IsKeyPressed("up") and CanWalk(px, py - 1, true)
    newY = py - 1
    didMove = true
  else if IsKeyPressed("down") and CanWalk(px, py + 1, true)
    newY = py + 1
    didMove = true
  end

  if didMove
    Text(newX + margin, newY + margin, "+")
    DisplayGraphics() # make the sword appear immediately
    bool lost = MaybeFight(newX, newY)
    if lost
      # do nothing
    else
      px = newX
      py = newY
    end
    Draw()
    AI()
    CheckForEdge()
  end
end

void AI()
  loop e in enemies

    string t = e["type"]

    if t == "."
      # dead
    else
      UpdateEnemy(e)
    end
  end
end

void UpdateEnemy(array e)
  number x = e["x"]
  number y = e["y"]
  number dx = 0
  number dy = 0

  if px < x
    dx = -1
  else if px > x
    dx = 1
  end
  
  if py < y
    dy = -1
  else if py > y
    dy = 1
  end

  if dx != 0 && dy != 0
    if Random() < 0.5
      dx = 0
    else
      dy = 0
    end
  end

  number newX = x + dx
  number newY = y + dy

  if newX == px and newY == py
    Color(1,0,0)
    Text(margin + px, margin + py, "+")
    Text(margin + x, margin + y, e["type"])
    loop a from 1 to 10
      Color(0, a / 10.0,1 - a / 10.0)
      Text(1, 0, "Enemy is attacking you!")
      DisplayGraphics()
      Sleep(0.1)
    end
  else if(CanWalk(newX, newY, false))
    e["x"] = newX
    e["y"] = newY
  end

  Draw()
end

# Returns true if you lost the battle
bool MaybeFight(number x, number y)
  if x < 0
    return false
  else if x > w - 1
    return false
  else if y < 0
    return false
  else if y > h - 1
    return false
  end

  var e = GetEnemyAt(x, y)
  if e["type"] == "X"
    return Fight(x, y, e)
  end
  return false
end

array GetEnemyAt(number x, number y)
  array noEnemy = []
  noEnemy["type"] = "none"
  
  loop e in enemies
    if e["x"] == x and e["y"] == y
      return e
    end
  end

  return noEnemy
end

bool Fight(number x, number y, array enemy)  
  loop a from 1 to 10
    Color(a / 10.0,1 - a / 10.0,0)
    Text(1, 0, "Attack!")
    DisplayGraphics()
    Sleep(0.1)
  end

  if Random() > 0.5
    enemy["type"] = "."
    Color(1,0,0)
    Text(1, 1,"Enemy defeated!")
    DisplayGraphics()
    Sleep(1)
    return false
  else
    Color(0.5,0.5,0.5)
    Text(1, 1, "You missed")
    DisplayGraphics()
    Sleep(1)
    return true
  end
end

void Draw()
  Text(0,0, "                          ")
  Text(0,1, "                          ")
  Text(0,2, "                          ")

  Color(1,1,1)
  loop y from 0 to h - 1
    loop x from 0 to w - 1
      var row = level[y]
      var tile = row[x]
      if tile == "."
        Color(0,1,0.2)
      else if tile == "#"
        Color(0.3,0.3,0.3)
      end
      Text(x + margin, y + margin, row[x])
    end
  end

  loop e in enemies
    Color(1,0.5,0)
    Text(margin + e["x"], margin + e["y"], e["type"])
  end
  
  Color(1,1,1)
  Text(px + margin, py + margin, "@")

  Color(1,1,1)
  Text(margin, margin + h + 2, "HP: " + hp)

  DisplayGraphics()
end

bool CanWalk(number x, number y, bool isPlayer)
  if x < 0
    return true
  else if x > w - 1
    return true
  else if y < 0
    return true
  else if y > h - 1
    return true
  end

  if isPlayer == false
    var e = GetEnemyAt(x, y)
    if e["type"] != "none"
      return false # enemy is blocking enemy
    end
  end

  var row = level[y]
  var tile = row[x]

  if tile == "#"
    return false
  end

  return true
end

void CheckForEdge()
  bool teleported = false

  if px < 0
    px = w - 1
    teleported = true
  else if px > w - 1
    px = 0
    teleported = true
  end

  if py < 0
    py = h - 1
    teleported = true
  else if py > h - 1
    py = 0
    teleported = true
  end

  if teleported
    enemies = []
    ClearText()
    GenerateLevel()
    SetLevel(px, py, " ")
    Draw()
  end
end

void GenerateLevel()
  loop y from 0 to h - 1
    level[y] = []
    loop x from 0 to w - 1
      SetLevel(x, y, RandomTerrain(levelRoughness))
    end
  end

  loop from 1 to 4
    PlaceEnemy()
  end
end

void SetLevel(number x, number y, string s)
  var row = level[y]
  row[x] = s
end

string RandomTerrain(number roughness)
   var r = Int(Random() * roughness)
   if r == 0
     return "."
   else if r < 4
     return " "
   else
     return "#"
   end
end

void PlaceEnemy()
  var x = Int(Random() * w)
  var y = Int(Random() * h)
  array enemy = []
  enemy["x"] = x
  enemy["y"] = y
  enemy["type"] = "X"
  Append(enemies, enemy)
  SetLevel(x, y, " ")
end



